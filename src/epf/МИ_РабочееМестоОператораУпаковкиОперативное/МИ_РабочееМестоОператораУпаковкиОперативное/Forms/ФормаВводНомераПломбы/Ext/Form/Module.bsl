
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НомерПломбы = "";
	УстановитьТекстПодсказки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить( "СканерШтрихкода" );
		Если Не МенеджерОборудованияКлиент.ПодключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО, ОписаниеОшибки) Тогда
			ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:%1""%2"".'" );
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Символы.ПС, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Отказ = Истина;
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить( "СканерШтрихкода" );
	МенеджерОборудованияКлиент.ОтключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	// Конец ПодключаемоеОборудование
	
	Оповестить("ЗакрытаФормаВводаНомераПломбы",, ВладелецФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[ 1 ] = Неопределено Тогда
				ТекКод = Параметр[ 0 ];
			Иначе
				ТекКод = Параметр[ 1 ][ 1 ];
			КонецЕсли;
			Если ЗначениеЗаполнено(ТекКод) Тогда
				ОбработатьШтрихКод(ТекКод);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВвестиНомерПломбыНажатие(Элемент)
	
	Закрыть(Новый Структура("НомерПломбы", НомерПломбы));
	
КонецПроцедуры

&НаКлиенте
Процедура ШтрихКодПриИзменении(Элемент)
	
	ОбработатьШтрихКод(ШтрихКод);
	ШтрихКод = "";
	ЭтаФорма.ТекущийЭлемент = Элементы.ШтрихКод;
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьШтрихКод(ШтрихКод)
	
	Если Не ЗначениеЗаполнено(ШтрихКод) Тогда
		Возврат;
	КонецЕсли;
	
	НомерПломбы = ШтрихКод;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстПодсказки()
	
	ТекстПодсказки = НСтр("ru = 'Отсканируйте номер пломбы или введите его в поле'");
	ТекстОшибки = "";
	
КонецПроцедуры

#КонецОбласти 