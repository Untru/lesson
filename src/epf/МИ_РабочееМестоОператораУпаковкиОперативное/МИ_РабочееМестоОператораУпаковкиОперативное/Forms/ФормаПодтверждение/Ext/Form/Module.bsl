
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЯчейкаКорзина      = Справочники.усЯчейки.ПустаяСсылка();
	сткДанныеНоменклатура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Номенклатура, "Наименование, НаименованиеТСД");
	Номенклатура       = усТСДОсновной.СформироватьПредставлениеНоменклатуры(Параметры.Номенклатура, сткДанныеНоменклатура.Наименование, сткДанныеНоменклатура.НаименованиеТСД);
	Количество         = Параметры.Количество;
	КонечныеКонтейнеры = Параметры.КонечныеКонтейнеры;
	
	УстановитьТекстПодсказки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		ОписаниеОшибки = "" ;
		ПоддерживаемыеТипыВО = Новый Массив();
		ПоддерживаемыеТипыВО.Добавить( "СканерШтрихкода" );
		Если Не МенеджерОборудованияКлиент.ПодключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО, ОписаниеОшибки) Тогда
			ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:%1""%2"".'" );
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Символы.ПС, ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Отказ = Истина;
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПоложитьКонтейнер Тогда
		Если ЗначениеЗаполнено(Корзина) Тогда
			ТекстОшибки = НСтр( "ru = 'Ошибка. Положите отсканированный контейнер-корзину.'" );	
		Иначе
			ТекстОшибки = НСтр( "ru = 'Ошибка. Не отсканирован контейнер-корзина.'" );
		КонецЕсли;
		Отказ = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыВО = Новый Массив();
	ПоддерживаемыеТипыВО.Добавить( "СканерШтрихкода" );
	МенеджерОборудованияКлиент.ОтключитьОборудованиеПоТипу(УникальныйИдентификатор, ПоддерживаемыеТипыВО);
	// Конец ПодключаемоеОборудование
	
	Оповестить("ЗакрытаФормаПодтверждения",, ВладелецФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И Не ЗначениеЗаполнено(Корзина) И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[ 1 ] = Неопределено Тогда
				ТекКод = Параметр[ 0 ];
			Иначе
				ТекКод = Параметр[ 1 ][ 1 ];
			КонецЕсли;
			Если ЗначениеЗаполнено(ТекКод) Тогда
				ОбработатьШтрихКод(ТекКод);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПоложитьТоварНажатие(Элемент)
	
	//ПоложитьТоварНаСервере();
	//Если ТекстОшибки = "" Тогда
	//	Закрыть();
	//КонецЕсли;
	ПоложитьКонтейнер = Истина;
	Закрыть(Новый Структура("Ячейка, Контейнер", ЯчейкаКорзина, Корзина));
	
КонецПроцедуры

&НаКлиенте
Процедура ШтрихКодПриИзменении(Элемент)
	
	ОбработатьШтрихКод(ШтрихКод);
	ШтрихКод = "";
	ЭтаФорма.ТекущийЭлемент = Элементы.ШтрихКод;
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьШтрихКод(ШтрихКод)
	
	Если Не ЗначениеЗаполнено(ШтрихКод) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьШтрихКодНаСервере(ШтрихКод);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьШтрихКодНаСервере(ШтрихКод)
	
	ТекстОшибки = "";
	
	сткВозврат = усТСДОсновной.НайтиКонтейнер(Штрихкод, Неопределено,,,,,, Истина);
	Если сткВозврат.Ошибка <> "" Тогда
		ТекстОшибки = сткВозврат.Ошибка;
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(сткВозврат.ДокументПривязки) Тогда
		ТекстОшибки = НСтр("ru = 'ОШИБКА. Контейнер зарезервирован'");
		Возврат;
	ИначеЕсли НЕ КонечныеКонтейнеры.НайтиПоЗначению(сткВозврат.Контейнер) = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'ОШИБКА. Контейнер уже выбран как конечный'");
		Возврат;
	КонецЕсли;
	
	Корзина = сткВозврат.Контейнер;
	Если ЗначениеЗаполнено(сткВозврат.Ячейка) Тогда
		ЯчейкаКорзина = сткВозврат.Ячейка;
	КонецЕсли;
	
	УстановитьТекстПодсказки();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстПодсказки()
	
	Если ЗначениеЗаполнено(Корзина) Тогда
		Элементы.ПоложитьТовар.Видимость = Истина;
		ТекстПодсказки  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'ПОЛОЖИТЕ %1%2'"), Символы.ПС, Номенклатура);
		ТекстПодсказки2 = "" + Количество + " " + ЕдиницаИзмерения;
		ТекстПодсказки3 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'в %1'"), Корзина);		
	Иначе
		Элементы.ПоложитьТовар.Видимость = Ложь;
		ТекстПодсказки = НСтр("ru = 'Отсканируйте контейнер-корзина'");
	КонецЕсли;
	
	ТекстОшибки = "";
	
КонецПроцедуры

#КонецОбласти 