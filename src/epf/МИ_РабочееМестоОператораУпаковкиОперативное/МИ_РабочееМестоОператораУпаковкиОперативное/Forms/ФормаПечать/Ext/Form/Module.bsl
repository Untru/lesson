
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Контейнер = Параметры.Контейнер;
	
	Если Не ЗначениеЗаполнено(Контейнер) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	РабочееМесто 		= МенеджерОборудованияСервер.ПолучитьРабочееМестоКлиента();
	ДанныеРабочегоМеста = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РабочееМесто, "Принтер, ПринтерДокументов");
	ПринтерДокументов 	= ДанныеРабочегоМеста.ПринтерДокументов;
	ПринтерЭтикеток 	= ДанныеРабочегоМеста.Принтер;
			
	ПечатьНаПринтер = усСервисныеФункции.ХранилищеНастроекРабочихМестЗагрузить(
	    РабочееМесто,
		ПланыВидовХарактеристик.усНастройкиРабочихМест.УпаковкаПечатьНаПринтер,
		Ложь);
		
	Элементы.КнопкаПечатьУпаковочногоЛиста.Видимость 	= Параметры.ПечатьУпаковочногоЛиста;
	Элементы.КнопкаПечатьЭтикеткиКонтейнера.Видимость 	= Параметры.ПечатьЭтикеткиКонтейнера;
	Элементы.КнопкаПечатьЭтикеткиТоваров.Видимость 		= Параметры.ПечатьЭтикеткиТоваров;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьУпаковочногоЛистаНажатие(Элемент)
	
	Если ПечатьНаПринтер Тогда
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Обработка.усРабочееМестоОператораУпаковкиОперативное", "УпаковочныйЛист", Контейнер);
	Иначе	
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.усРабочееМестоОператораУпаковкиОперативное",
		"УпаковочныйЛист",
		Контейнер,
		Неопределено,
		Неопределено
		);	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЭтикеткиКонтейнераНажатие(Элемент)
	
	Если ЗначениеЗаполнено(Контейнер) Тогда
		
		Если ПечатьНаПринтер Тогда
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Обработка.усРабочееМестоОператораУпаковкиОперативное", "ЭтикеткаКонтейнера", Контейнер);
		Иначе	
			
			АдресВХранилище = ПолучитьДанныеДляПечатиЭтикеток();
			СтруктураПараметры = Новый Структура("АдресВХранилище, ТипЭтикеток, ИмяТаблицыИсходныхДанных, ПечатьИзДокумента", АдресВХранилище, ПредопределенноеЗначение("Перечисление.усТипыШаблоновЭтикеток.Контейнеры"), "Контейнеры", Истина);
			
			ОткрытьФорму(
			"Обработка.усПечатьЭтикеток.Форма.Форма",
			СтруктураПараметры,      // Параметры
			,                        // Владелец
			УникальныйИдентификатор  // Уникальность
			);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КнопкаПечатьЭтикеткиТоваровНажатие(Элемент)

	Если ЗначениеЗаполнено(Контейнер) Тогда
		
		сткДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВладелецФормы.ЗаказНаОтгрузку, Новый Структура ("Контрагент, ШаблонЭтикетки", "Контрагент" , "Контрагент.ЭтикеткаТовара"));
		ДокументМаркировка = СоздатьЗадачуМаркировки(Контейнер, сткДанныеДокумента.Контрагент, ВладелецФормы.Исполнитель, ВладелецФормы.ЗаказНаОтгрузку, сткДанныеДокумента.ШаблонЭтикетки);
		
		Если Не ДокументМаркировка = Неопределено Тогда
			
			Если ПечатьНаПринтер Тогда
				ПараметрыПечати = Новый Структура("ШаблонЭтикеткиИзКонтрагента");
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер("Обработка.усРабочееМестоОператораУпаковкиОперативное", "ЭтикеткаТоваров", ДокументМаркировка, ПараметрыПечати);
			Иначе	
				ТипЭтикеток     = "";
				ТабличнаяЧасть  = "";
				ПараметрыВыполненияКоманды = Новый Структура("ШаблонЭтикеткиИзКонтрагента");
				АдресВХранилище = ПолучитьДанныеДляПечатиЭтикетокТоваров(ДокументМаркировка, ТипЭтикеток, ТабличнаяЧасть, ПараметрыВыполненияКоманды);
				
				СтруктураПараметры = Новый Структура("АдресВХранилище, ТипЭтикеток, ИмяТаблицыИсходныхДанных, ПечатьИзДокумента, Документ", АдресВХранилище, ТипЭтикеток, ТабличнаяЧасть, Истина, Неопределено);
				
				ОткрытьФорму(
				"Обработка.усПечатьЭтикеток.Форма.Форма",
				СтруктураПараметры,            // Параметры
				,                              // Владелец
				Новый УникальныйИдентификатор  // Уникальность
				);
				
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЗакрытьНажатие(Элемент)
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДляПечатиЭтикеток()
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Контейнер);
	
	Возврат Справочники.усКонтейнеры.ПолучитьДанныеДляПечатиЭтикеток(МассивОбъектов);
	
КонецФункции

&НаСервере
Функция СоздатьЗадачуМаркировки(Контейнер, Контрагент, Исполнитель, ДокументОснование = Неопределено, ШаблонЭтикетки = Неопределено)
	
	Возврат Обработки.усРабочееМестоОператораУпаковкиОперативное.СоздатьЗадачуМаркировки(Контейнер, Контрагент, Исполнитель, ДокументОснование, ШаблонЭтикетки);
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеДляПечатиЭтикетокТоваров(ПараметрКоманды, ТипЭтикеток, ТабличнаяЧасть, ПараметрыВыполненияКоманды)
		
	Возврат Документы.усМаркировка.ПолучитьДанныеДляПечатиЭтикеток(ПараметрКоманды, ТипЭтикеток, ТабличнаяЧасть, ПараметрыВыполненияКоманды);
	
КонецФункции