
НастройкиСоединения = ПланыОбмена.МИ_ОбменВМС_ЕРП.ПолучитьДанныеПодключенияУзлов();

Если НастройкиСоединения = Неопределено Тогда
	СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;
	ЗаписьЖурналаРегистрации("Обмен ESB 23:",УровеньЖурналаРегистрации.Предупреждение,,ОбъектОбработки.Ссылка,"Не найдены настройки узла обмена");
Иначе
	НастройкиСоединения.Следующий();
	Если НЕ НастройкиСоединения.ИспользоватьДляОбмена Тогда
		СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;
		ЗаписьЖурналаРегистрации("Обмен ESB 23:",УровеньЖурналаРегистрации.Предупреждение,,ОбъектОбработки.Ссылка,"Выключен обмен по узлу");
	КонецЕсли;	
КонецЕсли;	

Если СостояниеСообщения = Перечисления.сшпСтатусыСообщений.Обработано Тогда
	
	усОбменПоWebСервису.ЗаписатьВИдентификаторОбмена(ОбъектОбработки.Ссылка);
		
	Если СостояниеСообщения = Перечисления.сшпСтатусыСообщений.Обработано Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_Номенклатура
		|ИЗ
		|	(ВЫБРАТЬ
		|		усАктСверкиИнвентаризацииИзлишки.Номенклатура КАК Номенклатура
		|	ИЗ
		|		Документ.усАктСверкиИнвентаризации.Излишки КАК усАктСверкиИнвентаризацииИзлишки
		|	ГДЕ
		|		усАктСверкиИнвентаризацииИзлишки.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		усАктСверкиИнвентаризацииНедостачи.Номенклатура
		|	ИЗ
		|		Документ.усАктСверкиИнвентаризации.Недостачи КАК усАктСверкиИнвентаризацииНедостачи
		|	ГДЕ
		|		усАктСверкиИнвентаризацииНедостачи.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		усАктСверкиИнвентаризацииИзменениеКачества.Номенклатура
		|	ИЗ
		|		Документ.усАктСверкиИнвентаризации.ИзменениеКачества КАК усАктСверкиИнвентаризацииИзменениеКачества
		|	ГДЕ
		|		усАктСверкиИнвентаризацииИзменениеКачества.Ссылка = &Ссылка) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	усНоменклатура.Ссылка
		|ИЗ
		|	ВТ_Номенклатура КАК ВТ_Номенклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.усНоменклатура КАК усНоменклатура
		|		ПО ВТ_Номенклатура.Номенклатура = усНоменклатура.Ссылка
		|ГДЕ
		|	НЕ усНоменклатура.Код ПОДОБНО ""ИМ-%""";
		
		Запрос.УстановитьПараметр("Ссылка", ОбъектОбработки.Ссылка);
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СостояниеСообщения = Перечисления.сшпСтатусыСообщений.Обработано Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	усАктСверкиИнвентаризации.Ссылка КАК Ссылка,
		|	усАктСверкиИнвентаризации.Ссылка.ТипАктаСверки КАК ТипАктаСверки
		|ПОМЕСТИТЬ втАкты
		|ИЗ
		|	Документ.усАктСверкиИнвентаризации КАК усАктСверкиИнвентаризации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.усТипыАктаСверкиИнвентаризации КАК усТипыАктаСверкиИнвентаризации
		|		ПО усАктСверкиИнвентаризации.ТипАктаСверки = усТипыАктаСверкиИнвентаризации.Ссылка
		|ГДЕ
		|	усАктСверкиИнвентаризации.Ссылка = &ОбъектОбработкиОтбора
		|	И усАктСверкиИнвентаризации.Проведен
		|	И  (усТипыАктаСверкиИнвентаризации.Порядок = 4
		|	ИЛИ  усТипыАктаСверкиИнвентаризации.Порядок = 5)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втАкты.Ссылка КАК Ссылка,
		|	усИдентификаторыОбмена.Идентификатор КАК ID,
		|	втАкты.Ссылка.Номер КАК Number,
		|	втАкты.Ссылка.Дата КАК Date,
		|	ВЫБОР
		|		КОГДА втАкты.ТипАктаСверки = ЗНАЧЕНИЕ(Перечисление.усТипыАктаСверкиИнвентаризации.Прочее)
		|			ТОГДА ""Прочее""
		|		КОГДА втАкты.ТипАктаСверки = ЗНАЧЕНИЕ(Перечисление.усТипыАктаСверкиИнвентаризации.ИзменениеКачества)
		|			ТОГДА ""ИзменениеКачества""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК TypeAttribute,
		|	втАкты.Ссылка.Комментарий КАК Description,
		|	23 КАК ClassID
		//++ЧегодаевПС 23.06.2023 80778. 83072
		//Убрал условие и сделал предопределенный класс
		//++ЧегодаевПС 23.06.2023 80778. 83072
		|ИЗ
		|	втАкты КАК втАкты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.усИдентификаторыОбмена КАК усИдентификаторыОбмена
		|		ПО втАкты.Ссылка = усИдентификаторыОбмена.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НоменклатураПриходРасход.Номенклатура,
		|	НоменклатураПриходРасход.СтатусНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.усСтатусыНоменклатуры.ПустаяСсылка) КАК СтатусНоменклатурыНедостач,
		|	СУММА(НоменклатураПриходРасход.Количество) КАК Количество
		|ИЗ
		|	(ВЫБРАТЬ
		|		усАктСверкиИнвентаризацииИзлишки.Номенклатура КАК Номенклатура,
		|		усАктСверкиИнвентаризацииИзлишки.СтатусНоменклатуры КАК СтатусНоменклатуры,
		|		СУММА(усАктСверкиИнвентаризацииИзлишки.Количество) КАК Количество
		|	ИЗ
		|		втАкты КАК втАкты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.усАктСверкиИнвентаризации.Излишки КАК усАктСверкиИнвентаризацииИзлишки
		|			ПО втАкты.Ссылка = усАктСверкиИнвентаризацииИзлишки.Ссылка
		|	ГДЕ
		|		НЕ втАкты.ТипАктаСверки = ЗНАЧЕНИЕ(Перечисление.усТипыАктаСверкиИнвентаризации.ИзменениеКачества)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		усАктСверкиИнвентаризацииИзлишки.Номенклатура,
		|		усАктСверкиИнвентаризацииИзлишки.СтатусНоменклатуры
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		усАктСверкиИнвентаризацииНедостачи.Номенклатура,
		|		усАктСверкиИнвентаризацииНедостачи.СтатусНоменклатуры,
		|		СУММА(-усАктСверкиИнвентаризацииНедостачи.Количество)
		|	ИЗ
		|		втАкты КАК втАкты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.усАктСверкиИнвентаризации.Недостачи КАК усАктСверкиИнвентаризацииНедостачи
		|			ПО втАкты.Ссылка = усАктСверкиИнвентаризацииНедостачи.Ссылка
		|	ГДЕ
		|		НЕ втАкты.ТипАктаСверки = ЗНАЧЕНИЕ(Перечисление.усТипыАктаСверкиИнвентаризации.ИзменениеКачества)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		усАктСверкиИнвентаризацииНедостачи.Номенклатура,
		|		усАктСверкиИнвентаризацииНедостачи.СтатусНоменклатуры) КАК НоменклатураПриходРасход
		|ГДЕ
		|	НЕ НоменклатураПриходРасход.Номенклатура.Код ПОДОБНО ""ИМ-%""
		|
		|СГРУППИРОВАТЬ ПО
		|	НоменклатураПриходРасход.Номенклатура,
		|	НоменклатураПриходРасход.СтатусНоменклатуры
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	усАктСверкиИнвентаризацииИзменениеКачества.Номенклатура,
		|	усАктСверкиИнвентаризацииИзменениеКачества.СтатусНоменклатурыИзлишков,
		|	усАктСверкиИнвентаризацииИзменениеКачества.СтатусНоменклатурыНедостач,
		|	СУММА(усАктСверкиИнвентаризацииИзменениеКачества.Количество)
		|ИЗ
		|	втАкты КАК втАкты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.усАктСверкиИнвентаризации.ИзменениеКачества КАК усАктСверкиИнвентаризацииИзменениеКачества
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.усНоменклатура КАК усНоменклатура
		|			ПО усАктСверкиИнвентаризацииИзменениеКачества.Номенклатура = усНоменклатура.Ссылка
		|		ПО втАкты.Ссылка = усАктСверкиИнвентаризацииИзменениеКачества.Ссылка
		|			И (втАкты.ТипАктаСверки = ЗНАЧЕНИЕ(Перечисление.усТипыАктаСверкиИнвентаризации.ИзменениеКачества))
		|ГДЕ
		|	НЕ усНоменклатура.Код ПОДОБНО ""ИМ-%""
		|	И усАктСверкиИнвентаризацииИзменениеКачества.Количество > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	усАктСверкиИнвентаризацииИзменениеКачества.СтатусНоменклатурыИзлишков,
		|	усАктСверкиИнвентаризацииИзменениеКачества.СтатусНоменклатурыНедостач,
		|	усАктСверкиИнвентаризацииИзменениеКачества.Номенклатура";	
		
		Запрос.УстановитьПараметр("ОбъектОбработкиОтбора", ОбъектОбработки.Ссылка);
		
		Результат = Запрос.ВыполнитьПакет();
		
		// инвентаризация
		Если Не Результат[1].Пустой()
			И Не Результат[2].Пустой() Тогда  
			
			Выборка = Результат[1].Выбрать();
			// изменение качества				
			Выборка.Следующий();
			
			СтруктураДанных = Новый Структура("ClassID, ID, Number, Date, TypeAttribute, Description, WarehouseID, GoodsLine");
			ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка);	
			СтруктураДанных.WarehouseID = НастройкиСоединения.Склад;
			
			ТаблицаРезультатИК = Результат[2].Выгрузить();
			ТаблицаРезультатИК = усУправлениеСкладомСервер.РазложитьНаУпаковкиНоменклатуры(ТаблицаРезультатИК);
			
			ЗапросТЧ = Новый Запрос;
			ЗапросТЧ.Текст = 
			"ВЫБРАТЬ
			|	тбРезультат.Номенклатура КАК Номенклатура,
			|	тбРезультат.СтатусНоменклатуры,
			//++ЧегодаевПС 21.06.2023 80778. 81113
			|	тбРезультат.СтатусНоменклатурыНедостач,
			//--ЧегодаевПС 21.06.2023 80778. 81113
			|	тбРезультат.Количество
			|ПОМЕСТИТЬ втТабЧастьИК
			|ИЗ
			|	&тбРезультат КАК тбРезультат
			|ГДЕ 
			|тбРезультат.СтатусНоменклатуры <> &СтатусНоменклатурыНеПередаватьВАкте И тбРезультат.СтатусНоменклатурыНедостач <> &СтатусНоменклатурыНеПередаватьВАкте
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(axИдентификаторыОбменаНоменклатура.Идентификатор, ""00000000-0000-0000-0000-000000000000"") КАК NomenclatureID,
			|	ЕСТЬNULL(усНоменклатура.Наименование, """") КАК Nomenclature,
			|	ЕСТЬNULL(усСтатусыНоменклатуры.Наименование, """") КАК StatusItem,
			//++ЧегодаевПС 21.06.2023 80778. 81113
			|	ЕСТЬNULL(СтатусыНоменклатурыНедостач.Наименование, """") КАК StatusItemShortage,
			//--ЧегодаевПС 21.06.2023 80778. 81113
			|	втТабЧастьИК.Количество КАК Quantity
			|ИЗ
			|	втТабЧастьИК КАК втТабЧастьИК
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.усИдентификаторыОбмена КАК axИдентификаторыОбменаНоменклатура
			|		ПО втТабЧастьИК.Номенклатура = axИдентификаторыОбменаНоменклатура.Объект
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.усСтатусыНоменклатуры КАК усСтатусыНоменклатуры
			|		ПО втТабЧастьИК.СтатусНоменклатуры = усСтатусыНоменклатуры.Ссылка
			//++ЧегодаевПС 21.06.2023 80778. 81113
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.усСтатусыНоменклатуры КАК СтатусыНоменклатурыНедостач
			|		ПО втТабЧастьИК.СтатусНоменклатурыНедостач = СтатусыНоменклатурыНедостач.Ссылка
			//--ЧегодаевПС 21.06.2023 80778. 81113
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.усНоменклатура КАК усНоменклатура
			|		ПО втТабЧастьИК.Номенклатура = усНоменклатура.Ссылка
			//++ЧегодаевПС 21.06.2023 80778. 81113
			| ГДЕ ЕСТЬNULL(усСтатусыНоменклатуры.Наименование, """") <> ЕСТЬNULL(СтатусыНоменклатурыНедостач.Наименование, """")";
			//--ЧегодаевПС 21.06.2023 80778. 81113
			
			ЗапросТЧ.УстановитьПараметр("тбРезультат", ТаблицаРезультатИК);		
			
			НастройкиСервисаИнвентаризации = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить("СервисИнвентаризации", "СервисИнвентаризации",
			                                                                                      Новый Структура(),Неопределено, "ЛюбойПользователь");
			
			Если НастройкиСервисаИнвентаризации.Свойство("СтатусНоменклатурыНеПередаватьВАкте") Тогда
				ЗапросТЧ.УстановитьПараметр("СтатусНоменклатурыНеПередаватьВАкте", НастройкиСервисаИнвентаризации.СтатусНоменклатурыНеПередаватьВАкте);
			Иначе
				ЗапросТЧ.УстановитьПараметр("СтатусНоменклатурыНеПередаватьВАкте", Справочники.усСтатусыНоменклатуры.ПустаяСсылка());
			КонецЕсли;	
			
			ВыборкаИК = ЗапросТЧ.Выполнить().Выбрать();										
			
			// табличная часть
			ФиксСтруктураКлассаТовары = "NomenclatureID, StatusItem, StatusItemShortage, Nomenclature, Quantity";		
			GoodsLine                 = Новый Массив;
			
			Пока ВыборкаИК.Следующий() Цикл 
				СтруктураДанныхТовары = Новый Структура(ФиксСтруктураКлассаТовары);
				ЗаполнитьЗначенияСвойств(СтруктураДанныхТовары, ВыборкаИК);	
				GoodsLine.Добавить(СтруктураДанныхТовары);				
			КонецЦикла;
			СтруктураДанных["GoodsLine"] = GoodsLine;
			
			РезультатОбработки.Body    = сшпОбщегоНазначения.ПреобразоватьСтруктуруПоФормату(, СтруктураДанных);	
			РезультатОбработки.ClassID = Выборка.ClassID;
			РезультатОбработки.Receivers.Добавить("ERP");			
			
		Иначе 
			СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;		
		КонецЕсли;
		
	КонецЕсли;
КонецЕсли;	